apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'application'

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

mainClassName = 'com.github.hisahi.tiralabradesaes.ui.Main'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.10'
}

jacoco {
    toolVersion = "0.7.4.201502262128"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/ui/**'])
        })
    }
}

run {
    standardInput = System.in
}

jar {
    manifest {
        attributes (
            'Main-Class': mainClassName
        )
    }
}

task sourcesJavadocJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.output
    from sourceSets.main.allSource
    from javadoc.destinationDir
    manifest {
        attributes (
            'Main-Class': mainClassName
        )
    }
}

artifacts {
    archives sourcesJavadocJar
}

check.dependsOn jacocoTestReport

